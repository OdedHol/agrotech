#include <OneWire.h>
#include <DallasTemperature.h>
#include <Wire.h>
#include "Adafruit_SHT31.h"
#include <WiFi.h>
#include "ThingSpeak.h"

// GPIO where the DS18B20 is connected to
const int oneWireBus = A4;

// Setup a oneWire instance to communicate with any OneWire devices
OneWire oneWire(oneWireBus);

// Pass our oneWire reference to Dallas Temperature sensor
DallasTemperature sensors(&oneWire);

Adafruit_SHT31 sht31 = Adafruit_SHT31();

// Set the ThingSpeak channel and API key information
unsigned long myChannelNumber = 2184926;
const char* myWriteAPIKey = "JDZ7WLK4UNQE5CEW";

// Set the WiFi network credentials
const char* ssid = "Syd"; // your wifi SSID name
const char* password = "0524371802"; // wifi password

// Set the ThingSpeak server address
const char* server = "api.thingspeak.com";

// Create a WiFiClient object to connect to the WiFi network
WiFiClient client;

// Set the time to wait between uploading data to ThingSpeak (in milliseconds)
int wait_between_uploads = 10000; // 10 seconds

void setup() {
  // Start the serial communication at 112500 baud
  Serial.begin(112500);

  // Start the DS18B20 sensor
  sensors.begin();

  if (!sht31.begin(0x44)) {
    Serial.println("Couldn't find SHT31");
    while (1) delay(1);
  }

  // Connect to the WiFi network
  WiFi.begin(ssid, password);
  Serial.print("Connecting to ");
  Serial.println(ssid);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.print("ESP32 connected to WiFi: ");
  Serial.println(ssid);
  Serial.println();

  // Initialize the ThingSpeak library with the WiFi client
  ThingSpeak.begin(client);
}

void loop() {
  // Read sensor values
  sensors.requestTemperatures();
  float dsTemperatureC = sensors.getTempCByIndex(0);
  float dsTemperatureF = sensors.getTempFByIndex(0);

  float shtTemperature = sht31.readTemperature();
  float shtHumidity = sht31.readHumidity();

  if (!isnan(dsTemperatureC)) {
    Serial.print("DS Temp *C = ");
    Serial.println(dsTemperatureC);
  } else {
    Serial.println("Failed to read DS temperature");
  }

  if (!isnan(dsTemperatureF)) {
    Serial.print("DS Temp *F = ");
    Serial.println(dsTemperatureF);
  } else {
    Serial.println("Failed to read DS temperature");
  }

  if (!isnan(shtTemperature)) {
    Serial.print("SHT Temp *C = ");
    Serial.println(shtTemperature);
  } else {
    Serial.println("Failed to read SHT temperature");
  }

  if (!isnan(shtHumidity)) {
    Serial.print("SHT Hum. % = ");
    Serial.println(shtHumidity);
  } else {
    Serial.println("Failed to read SHT humidity");
  }

  // Set the values to be sent to ThingSpeak
  ThingSpeak.setField(1, dsTemperatureC);
  ThingSpeak.setField(2, dsTemperatureF);
  ThingSpeak.setField(3, shtTemperature);
  ThingSpeak.setField(4, shtHumidity);

  // Send the data to ThingSpeak
  ThingSpeak.writeFields(myChannelNumber, myWriteAPIKey);

  // Print a message to the serial monitor indicating that the data has been uploaded
  Serial.println("Uploaded to ThingSpeak server.");

  // Disconnect the WiFi client
  client.stop();

  // Wait for the specified amount of time before uploading the next set of data
  Serial.println("Waiting to upload next reading...");
  Serial.println();

  delay(wait_between_uploads);
}
